//server-only
global procedure DeadItemsRn(record RcVc RepSpec)
begin
	record ItemHistVc IHr;
	record INVc INr;
	record BPIBrandVc BPIBrandr;
	record ItemStatusVc ISr;
	record IVVc IVr;
	record SHVc SHr;
	vector val salesbystock,instockbystock;
	boolean testf,TrHs,testf1;
	array string 100 salestags,stocktags;
	integer i;
	
	startreportnoheaderjob("Dead Stock Report");
	
	if(nonblank(RepSpec.f1))then begin
		BPIBrandr.Code = RepSpec.f1;
		if(readfirstmain(BPIBrandr,1,true))then begin
			while(loopmain(INr,1,true))begin
				testf = true;
				if(INr.BPIBrand!=BPIBrandr.Code)then begin testf = false; end;
				
				if(testf)then begin
					ClearVector(salesbystock);
					ClearVector(instockbystock);
					ISr.Code = INr.Code;
					TrHs = true;
					while(loopmain(ISr,1,TrHs))begin
						if(ISr.Code!=INr.Code)then begin TrHs = false; end;
						
						if(TrHs)then begin
							instockbystock[ISr.Location] = ISr.Instock;
						end;
					end;
					resetloop(ISr);
					IHr.ArtCode = INr.Code;
					IHr.TransDate = currentdate;
					TrHs = true;
					while(loopbackkey("ArtCode",IHr,2,TrHs))begin
						testf1 = true;
						if(IHr.TransDate<RepSpec.sStartDate)then begin TrHs = false; testf1 = false; end;
						if(IHr.ArtCode!=INr.Code)then begin TrHs = false; testf1 = false; end;
						if(IHr.StockAffectf==0)then begin testf1 = false; end;
						
						if(testf1)then begin
							instockbystock[IHr.Location] = instockbystock[IHr.Location] - IHr.Qty;
							if(dateinrange(IHr.TransDate,RepSpec.sStartDate,RepSpec.sEndDate))then begin
								if(IHr.FileName=="IVVc")then begin
									IVr.SerNr = IHr.TransNr;
									if(readfirstmain(IVr,1,true))then begin
										if(IVr.Invalid==0)then begin
											if(left(IVr.CustCode,3)!="FOB")then begin
												salesbystock[IHr.Location] = salesbystock[IHr.Location] - IHr.Qty;
											end;
										end;
									end;
								end;
								if(IHr.FileName=="SHVc")then begin
									SHr.SerNr = IHr.TransNr;
									if(readfirstmain(SHr,1,true))then begin
										if(left(SHr.CustCode,3)!="FOB")then begin
											salesbystock[IHr.Location] = salesbystock[IHr.Location] - IHr.Qty;
										end;
									end;
								end;
							end;
							
						end;
					end;
					resetloop(IHr);
					
					getvectortags(instockbystock,stocktags);
					getvectortags(salesbystock,salestags);
					if(stocktags.length>0)then begin
						for(i=0;i<stocktags.length;i=i+1)begin
							if(instockbystock[stocktags[i]]!=0)then begin
								startformat(15);
									outstring(0,0,INr.Code,false);
									outstring(80,0,INr.Name,false);
									outstring(180,0,instockbystock[stocktags[i]],false);
									outstring(280,0,salesbystock[stocktags[i]],false);
									if(salesbystock[stocktags[i]]==0)then begin
										outstring(280,0,"Dead Item",false);
									end;
								endformat;
							end;
						end;
					end;
					
				end;
	
			end;
		end;
	end;
	
	
	endjob;

return
end;